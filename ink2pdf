#!/usr/bin/env perl
use strict;
use warnings;

use v5.10.0;
use File::Spec;
use Getopt::Long;
use Pod::Usage;
use XML::LibXML;
use XML::LibXML::XPathContext;

# Inits
#<<<
my $opts = {};
GetOptions($opts,
    "background|b:s",
    "help|h",
    "insert|i:s@",
    "keep|k",
    "show-layers|l",
    "show-mapping|m",
    "output|o:s",
    "tags|t:s",
    "verbose|v",
    "prefixes|x",
    ) or pod2usage(-verbose => 0,
                   -exitval => 22,  # 22 = EINVAL Invalid argument
                   -message => "*** Invalid option or bad syntax");
#>>>
pod2usage(
    -verbose => 2,
    -exitval => 0
) if $opts->{help};
pod2usage(
    -verbose => 0,
    -exitval => 2,                                    # 2 = ENOENT NO such file
    -message => "*** Missing Inkscape SVG filename"
) if !@ARGV;
my $SVGFILE     = $ARGV[0];
my $BGLAYER     = $opts->{background} || q{};
my $KEEP        = $opts->{keep};
my $SHOWLAYERS  = $opts->{'show-layers'};
my $SHOWMAPPING = $opts->{'show-mapping'};
my @CTAGS       = split(',', $opts->{tags} || q{});
my $VERBOSE     = $opts->{verbose};
my $XMODE       = $opts->{prefixes};
die "*** Conditional tags may only be used with --prefixes (-x)\n"
    if @CTAGS && !$XMODE;

# Load file
my $xml   = XML::LibXML->load_xml(location => $SVGFILE);
my $xpath = XML::LibXML::XPathContext->new($xml);
$xpath->registerNs("defNs", 'http://www.w3.org/2000/svg');

# Create related file names
my (undef, undef, $base) = File::Spec->splitpath($SVGFILE);
$base =~ s{\.svg$}{}i;
my $outfile = $opts->{output} || "$base.pdf";

# Get layer names
my @LAYERS = get_layers($xpath);
die "*** No layers in file $SVGFILE\n" unless @LAYERS;
if ($SHOWLAYERS) {
    print map {"$_\n"} @LAYERS;
    exit 0;
}
if ($BGLAYER) {
    unless (grep {$_ eq $BGLAYER} @LAYERS) {
        warn "--- No such background layer: '$BGLAYER'\n";
        $BGLAYER = q{};
    }
}

# Generate the PDF's
$XMODE
    ? do_by_prefix()
    : do_all_layers();

exit 0;

#
#                           ------- o -------
#

# Do all layers: Simply map each layer to a page (with a b/g option)
sub do_all_layers {

    # Create layer set
    my @lset = ();
    foreach my $layer (@LAYERS) {
        next if $layer eq $BGLAYER;
        push @lset, [$layer, $BGLAYER || ()];
    }
    return show_page_mapping(@lset) if $SHOWMAPPING;

    # Generate the output
    generate_all(@lset);
}

# Create pages as indicated by layer prefixes
sub do_by_prefix {

    # Find all prefixes
    my $seq_re   = qr{ \* | L | R | [0-9\.\-]+ }ix;
    my $tag_re   = qr{ [a-z0-9\-]+ }ix;
    my @prefixen = ();
    say "Discovered Prefixen:" if $VERBOSE;
    foreach my $layer (@LAYERS) {
        next unless $layer =~ m{^\s* ([pbc])                                # Start char
                                     ($seq_re (?: \, $seq_re)* )?           # Seqnums
                                     (?: \( ($tag_re (?: \, $tag_re)* ) )?  # Optional tags
                                         \)                                 # Required closing paren
                               }ix;
        my $pset = [lc $1, lc($2 || q{}), lc($3 || q{}), $layer];    # pbc, seq, ctag, layername
        push @prefixen, $pset;
        say "  type: $pset->[0]  seqs: "
            . ($pset->[1] || q{--})
            . "\ttags: "
            . ($pset->[2] || q{--})
            . "\tlayer: '$pset->[3]'"
            if $VERBOSE;
    }
    say "" if $VERBOSE;
    die "*** No layer prefixes discovered -- output would be empty (don't use -x?)\n"
        unless @prefixen;

    # Get content sequence numbers
    my %cseqs = ();
    foreach my $x (@prefixen) {
        next if $x->[0] eq 'b';    # skip forced background layers
        my $seq = $x->[1];
        next unless $seq;                       # skip blank
        next unless $seq =~ m{^[0-9\.\,]+$};    # can't be a b/g layer (no dash, no *, ...)
        foreach my $sq (split(/,/, $seq)) {
            if ($sq !~ m{^\d+(\.\d*)?$}) {
                warn "--- Skip bad sequence number '$sq' in layer name '$x->[3]'\n";
                next;
            }
            $cseqs{$sq}++;
        }
    }
    my @cseq = sort {$a <=> $b} keys %cseqs;    # Sorted numerically

    # Build layer-sets list
    my @l2p  = ();
    my $pnum = 1;
    for my $sq (@cseq) {

        # Find all layers that match
        my $any = 0;
        foreach my $x (@prefixen) {
            if (seq_match($sq, $x->[1], $pnum)) {               # TODO: ctag match
                my $lname = $x->[3];
                if ($any) {
                    # Add to current page
                    push @{$l2p[-1]}, $lname;
                }
                else {
                    # Start new page
                    push @l2p, [$lname];
                    $any = 1;
                }
            }
        }
        ++$pnum if $any;
    }
    die "*** No content in output\n" unless @l2p;
    return show_page_mapping(@l2p) if $SHOWMAPPING;

    # Generate output
    generate_all(@l2p);
}

# Generate all output
sub generate_all {
    my @l2p = @_;

    say "Page Generation..." if $VERBOSE;
    my $pnum      = 0;
    my @pdf_files = ();
    foreach my $lset (@l2p) {
        ++$pnum;

        # Create temp SVG with desired layers set
        my $tmpfile = sprintf("$base-%3.3d.svg", $pnum);
        say "  Temp SVG $tmpfile" if $VERBOSE;
        my $n = set_layers_visible($xpath, @$lset);
        warn "--- No matching layers for:\n" . (map {"\t$_\n"} @$lset) unless $n;
        $xml->toFile($tmpfile);

        # PDF generation
        my $pdffile = sprintf("$base-%3.3d.pdf", $pnum);
        push @pdf_files, $pdffile;
        generate_pdf($tmpfile, $pdffile);
        say "   --> PDF $pdffile generated" if $VERBOSE;

        # Cleanup
        unlink $tmpfile;
    }

    # Unify PDF's
    if ($KEEP) {
        say "Individual PDF files will be kept (-k)" if $VERBOSE;
    }
    else {
        # ... unify ...
        say "  Unifying pages" if $VERBOSE;
        system("pdfunite " . join(" ", map {"'$_'"} @pdf_files) . " '" . $outfile . "'") == 0
            or die "*** pdfunite command failure\n";
        say "  Cleanup" if $VERBOSE;
        unlink @pdf_files;
        say "Created $outfile" if $VERBOSE;
    }
}

# Create a one-page PDF
sub generate_pdf {
    my $inkfile = shift;
    my $outfile = shift;
    system("inkscape -z -A '$outfile' '$inkfile' 2>/dev/null") == 0
        or die "*** inkscape command failure\n";
}

# Get layer names (technically, called "labels")
sub get_layers {
    my $xpath  = shift;
    my @labels = ();
    foreach my $layer ($xpath->findnodes('/defNs:svg/defNs:g[@inkscape:groupmode="layer"]')) {
        my $label = $layer->{'inkscape:label'};
        push @labels, $label;
    }
    return @labels;
}

# Does a given sequence match a sequence spec?
sub seq_match {
    my $want = shift;
    my $spec = shift;
    my $pnum = shift;

    foreach my $one (split(/,/, $spec)) {
        return 1 if $one eq '*';
        return 1 if ($one eq 'l') && ($pnum % 2) && ($pnum != 1);    # Left-side doesn't include p1
        return 1 if ($one eq 'r') && !($pnum % 2);
        return 1 if $one eq $want;

        # TODO: Ranges
    }

    return 0;
}

# Set the given layers visible, hide the others
sub set_layers_visible {
    my $xpath = shift;
    my @toset = @_;

    my $numset = 0;
LAYER:
    foreach my $layer ($xpath->findnodes('/defNs:svg/defNs:g[@inkscape:groupmode="layer"]')) {
        my $label = $layer->{'inkscape:label'};
        foreach my $wantlabel (@toset) {
            if ($label eq $wantlabel) {
                $layer->{'style'} =~ s{\bdisplay:none\b}{display:inline};
                ++$numset;
                next LAYER;
            }
        }
        $layer->{'style'} =~ s{\bdisplay:inline\b}{display:none};
    }
    return $numset;
}

# Show the layer-to-page mapping grid
sub show_page_mapping {
    my @lsets = @_;    # Sets of layers in page order: ([lb,l1], [lb,l2,l5], [l3,lb], ...)

    # Print heading
    my $pmax = scalar(@lsets);
    if ($pmax > 99) {
        print " " x 40;
        for my $p (1 .. $pmax) {
            print(int($p / 100) || ".");
        }
        print "\n";
    }
    if ($pmax > 9) {
        print " " x 40;
        for my $p (1 .. $pmax) {
            print((int($p / 10) % 10) || ".");
        }
        print "\n";
    }
    print "  Layer Name                Page # -->  ";
    for my $p (1 .. $pmax) {
        print $p % 10;
    }
    print "\n";
    say "  ==================================== +" . ('=' x $pmax) . "+";

    # Show mapping for ALL layers even those not in the layer set
    my $pnum = 0;
    foreach my $lname (@LAYERS) {
        my $l36
            = length($lname) <= 36
            ? $lname
            : substr($lname, 0, 35) . 'â€¦';
        printf("  %-36s |", $l36);
        for my $p (1 .. $pmax) {
            my @lset = @{$lsets[$p - 1]};
            print((grep {$lname eq $_} @lset) ? '*' : '.');
        }
        print "|\n";
    }
    say "  ==================================== +" . ('=' x $pmax) . "+";
}

__END__

=head1 NAME

ink2pdf - Inkscape to Multi-Page PDF Document

=head1 SYNOPSIS

 ink2pdf [options] inkscape-file

 Options:
  -b  --background L   Use layer L as a backround layer (cannot use with -x)
  -h  --help           Usage summary
  -i  --insert N:file  Insert existing PDF file into document at page N; 
                         can use multiple times
  -k  --keep           Don't unite the pages into one PDF; keep each page 
                         as it's own numbered .pdf file
  -l  --show-layers    Show layer names and exit
  -m  --show-mapping   Show layer-to-page mapping and exit
  -o  --output PDFFILE Specify the output PDF file name; default is to 
                         replace the ".svg" suffix of the input file name 
                         with ".pdf"; if no ".svg" suffix then ".pdf" is 
                         appended.
  -t  --tags TAGS      Conditional tags in effect; separate tags with commas
  -x  --prefixes       Parse prefixes in layer names
  -v  --verbose        Verbose mode

=head1 DESCRIPTION

Generates a multi-page PDF document from your Inkscape document.
By default each layer becomes a page; use "page prefixes" in your
Inkscape document and the --prefixes (-x) option for better control.

=cut
